ВЫБРАТЬ
	ДипломДанныеКатегорий.Ссылка.Студент КАК Студент,
	ДипломДанныеКатегорий.Категория КАК Категория,
	СРЕДНЕЕ(ДипломДанныеКатегорий.Оценка.ПриоритетОценки) КАК Оценка,
	ВЫБОР
		КОГДА ДипломДанныеКатегорий.Ссылка.Специальность.Наименование ПОДОБНО "%педагогич%"
			ТОГДА 1
		ИНАЧЕ 0
	КОНЕЦ КАК ПедОбразование,
	ГОД(ДипломДанныеКатегорий.Ссылка.ГодЗавершения) КАК ГодЗавершения
ПОМЕСТИТЬ втСредниеБаллы
ИЗ
	Документ.Диплом.ДанныеКатегорий КАК ДипломДанныеКатегорий
ГДЕ
	ДипломДанныеКатегорий.Оценка.СистемаОценивания = &СистемаОценивания
	И ДипломДанныеКатегорий.Ссылка.УровеньПодготовки = &УровеньПодготовки
	И ДипломДанныеКатегорий.Ссылка.ФормаОбученияПриЗавершении = &ФормаОбучения
	И ГОД(ДипломДанныеКатегорий.Ссылка.ГодЗавершения) МЕЖДУ 2015 И 2021
	//И ГОД(ДипломДанныеКатегорий.Ссылка.ГодЗавершения) = 2022
	И НЕ ДипломДанныеКатегорий.Ссылка.ДатаВыдачи = &ДатаВыдачи
	И НЕ ДипломДанныеКатегорий.Ссылка.ПометкаУдаления

СГРУППИРОВАТЬ ПО
	ДипломДанныеКатегорий.Категория,
	ДипломДанныеКатегорий.Ссылка.Студент,
	ВЫБОР
		КОГДА ДипломДанныеКатегорий.Ссылка.Специальность.Наименование ПОДОБНО "%педагогич%"
			ТОГДА 1
		ИНАЧЕ 0
	КОНЕЦ,
	ГОД(ДипломДанныеКатегорий.Ссылка.ГодЗавершения)
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	втСредниеБаллы.Студент КАК Студент,
	втСредниеБаллы.Студент.Пол КАК Пол,
	втСредниеБаллы.Студент.ДатаРождения КАК ДатаРождения,
	втСредниеБаллы.ГодЗавершения КАК Год,
	втСредниеБаллы.ПедОбразование КАК ПедОбразование,
	СУММА(ВЫБОР
			КОГДА втСредниеБаллы.Категория = ЗНАЧЕНИЕ(Перечисление.КатегорииДипломов.КурсовыеРаботы)
				ТОГДА втСредниеБаллы.Оценка
		КОНЕЦ) КАК КурсовыеРаботы,
	СУММА(ВЫБОР
			КОГДА втСредниеБаллы.Категория = ЗНАЧЕНИЕ(Перечисление.КатегорииДипломов.Дисциплины)
				ТОГДА втСредниеБаллы.Оценка
		КОНЕЦ) КАК Дисциплины,
	СУММА(ВЫБОР
			КОГДА втСредниеБаллы.Категория = ЗНАЧЕНИЕ(Перечисление.КатегорииДипломов.ДипломныеРаботы)
				ТОГДА втСредниеБаллы.Оценка
		КОНЕЦ) КАК ДипломныеРаботы,
	СУММА(ВЫБОР
			КОГДА втСредниеБаллы.Категория = ЗНАЧЕНИЕ(Перечисление.КатегорииДипломов.Практики)
				ТОГДА втСредниеБаллы.Оценка
		КОНЕЦ) КАК Практики
ИЗ
	втСредниеБаллы КАК втСредниеБаллы

СГРУППИРОВАТЬ ПО
	втСредниеБаллы.Студент,
	втСредниеБаллы.ПедОбразование,
	втСредниеБаллы.ГодЗавершения,
	втСредниеБаллы.Студент.Пол,
	втСредниеБаллы.Студент.ДатаРождения